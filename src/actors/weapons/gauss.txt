// --------------------------------------------------------------------------
//
// ARGENT: Gauss Cannon
//
// --------------------------------------------------------------------------

ACTOR ArgGaussCannon : ArgWeapon
{
	Weapon.UpSound "weapons/gauss/equip"
	Weapon.SelectionOrder 3
	Weapon.AmmoUse 20
	Weapon.AmmoGive 40
	Weapon.AmmoType "Cell"
	Inventory.Icon "graphics/hud/weapon-gauss.png"
	Inventory.PickupMessage "$ARG_GOT_GAUSS"
	Tag "$ARG_TAG_GAUSS"
	Obituary "$ARG_OB_GAUSS"
	Decal "Scorch" // need to put this on the weapon since it doesn't work on rail puffs; dammit Xaser D:
	
	States
	{
	Spawn:
		GAUS A -1
		Stop
	Ready:
		GAUG FFFFFFFFEEDDCCBBAABBCCDDEE 1 A_WeaponReady
		Loop
	Select:
		GAUG A 0
		Goto SelectLoop
	Deselect:
		GAUG A 0
		Goto DeselectLoop
	Fire:
		GAUF A 1 Bright {
			A_GunFlash;
			A_Recoil(7.5);
			A_RailAttack (10*random(1,8), 8, 0, "none", "none", RGF_SILENT | RGF_NOPIERCING, 4, "ArgGaussPaul", 0, 0, 0, 0, 16, 0, "none"         , 0);
			A_RailAttack (10*random(1,8), 8, 0, "none", "none", RGF_SILENT | RGF_NOPIERCING, 4, "ArgGaussPaul", 0, 0, 0, 0, 16, 0, "none"         , 0);
			A_RailAttack (10*random(1,8), 8, 0, "none", "none", RGF_SILENT | RGF_NOPIERCING, 4, "ArgGaussPaul", 0, 0, 0, 0, 16, 0, "none"         , 0);
			A_RailAttack (10*random(1,8), 8, 0, "none", "none", RGF_SILENT | RGF_NOPIERCING, 4, "ArgGaussPaul", 0, 0, 0, 0, 16, 0, "none"         , 0);
			A_RailAttack (10*random(1,8), 8, 0, "none", "none", RGF_SILENT | RGF_NOPIERCING, 4, "ArgGaussPaul", 0, 0, 0, 0, 16, 0, "none"         , 0);
			A_RailAttack (10*random(1,8), 8, 0, "none", "none", RGF_SILENT | RGF_NOPIERCING, 4, "ArgGaussPaul", 0, 0, 0, 0, 16, 0, "none"         , 0);
			A_RailAttack (10*random(1,8), 8, 0, "none", "none", RGF_SILENT | RGF_NOPIERCING, 4, "ArgGaussPaul", 0, 0, 0, 0, 16, 0, "none"         , 0);
			A_RailAttack (10*random(1,8), 8, 0, "none", "none", RGF_SILENT | RGF_NOPIERCING, 4, "ArgGaussPaul", 0, 0, 0, 0, 16, 0, "none"         , 0);
			A_RailAttack (10*random(1,8), 8, 0, "none", "none", RGF_SILENT | RGF_NOPIERCING, 4, "ArgGaussPaul", 0, 0, 0, 0, 16, 0, "none"         , 0);
			A_RailAttack (10*random(1,8), 8, 1, "none", "none", RGF_SILENT | RGF_NOPIERCING, 4, "ArgGaussPuff", 0, 0, 0, 0, 16, 0, "ArgGaussTrail", 0);
			A_PlaySound("weapons/gauss/fire", CHAN_5);
			A_WeaponOffset(0.0, 32.0);
		}
		GAUF B 1 Bright A_WeaponOffset( 2.0, 34.0)
		GAUF C 1 Bright A_WeaponOffset( 4.0, 38.0)
		GAUF D 1 Bright A_WeaponOffset( 6.0, 42.0)
		GAUF E 1 Bright A_WeaponOffset( 8.0, 44.5)
		GAUF F 1 Bright A_WeaponOffset( 9.0, 46.5)
		GAUG E 1        A_WeaponOffset( 9.5, 47.5)
		GAUG E 1        A_WeaponOffset(10.0, 48.0)
		GAUG D 1        A_WeaponOffset( 9.0, 47.0)
		GAUG D 1        A_WeaponOffset( 8.0, 45.0)
		GAUG C 1        A_WeaponOffset( 6.5, 42.0)
		GAUG C 1        A_WeaponOffset( 5.0, 38.0)
		GAUG B 1        A_WeaponOffset( 3.0, 35.0)
		GAUG B 1        A_WeaponOffset( 2.0, 34.5)
		GAUG A 1        A_WeaponOffset( 1.0, 33.0)
		GAUG A 1        A_WeaponOffset( 0.5, 32.5)
		GAUG A 1        A_WeaponOffset( 0.0, 32.0)
		GAUG A 35 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		GAUG BCDE 2 A_WeaponReady(WRF_NOFIRE)
		GAUG A 0 A_Refire
		Goto Ready
	Flash:
		TNT1 A 2 Bright A_Light1
		TNT1 A 2 Bright A_Light(3)
		TNT1 A 2 Bright A_Light2
		TNT1 A 2 Bright A_Light1
		Goto LightDone
	}
}

ACTOR ArgGaussTrail : ArgTrail
{
	RenderStyle "Add"
	Alpha 1.0
	Scale 0.5
	States
	{
	Spawn:
		GAUT ABCD 2 Bright
		Stop
	}
}

ACTOR ArgGaussPuff : ArgPuff
{
	+ALWAYSPUFF
	+EXTREMEDEATH
	RenderStyle "Add"
	DamageType "ArgGaussCannon"
	Alpha 1.0
	Scale 0.75
	States
	{
	Spawn:
		GAUP A 2 Bright NoDelay A_PlaySound("weapons/gauss/impact")
		GAUP BCDEF 2 Bright
		Stop
	}
}

ACTOR ArgGaussPaul : ArgGaussPuff
{
	// it's Paul!
	States
	{
	Spawn:
		TNT1 A 0
		Stop
	}
}

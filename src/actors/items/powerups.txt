// --------------------------------------------------------------------------
//
// ARGENT: Powerups
//
// --------------------------------------------------------------------------

ACTOR ArgPowerup : CustomInventory
{
	Inventory.PickupSound "items/powerup"
	Inventory.MaxAmount 0

	+COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.FANCYPICKUPSOUND
}

// -- Berserk --

ACTOR ArgPowerBerserk : PowerDamage
{
    DamageFactor "ArgFist", 10.0
	Inventory.Icon "ICONBZRK"
	Powerup.Duration -30
	Powerup.Colormap 1.0,0.0,0.0, 0.0,0.0,0.0
}

ACTOR ArgPowerBerserkInvulnerable : PowerInvulnerable
{
	Powerup.Duration -30
}

ACTOR ArgPowerBerserkSpeed : PowerSpeed
{
    Speed 1.5
	Powerup.Duration -30
}

ACTOR ArgKILLTHEMALLWITHPUNCHING : Berserk replaces InvulnerabilitySphere
{
	Inventory.PickupMessage "$ARG_GOT_BERSERK"
	Inventory.PickupSound "items/powerup"
	
	States
	{
	Spawn:
		BZRK ABCDEFEDCB 3 Bright
		Loop
	Pickup:
		TNT1 A 0 A_PlaySound("items/powerups/berserk", CHAN_7)
		TNT1 A 0 A_GiveInventory("ArgPowerBerserk")
		TNT1 A 0 A_GiveInventory("ArgPowerBerserkInvulnerable")
		TNT1 A 0 A_GiveInventory("ArgPowerBerserkSpeed")
		TNT1 A 0 HealThing(100, 0)
		TNT1 A 0 A_SelectWeapon("ArgFist")
		Stop
	}
}

// -- Quad --

ACTOR ArgPowerQuadDamage : PowerDamage
{
	Inventory.Icon "ICONQUAD"
	DamageFactor "Normal", 4
	Powerup.Duration -45
	Powerup.Color "AA00FF" 0.25
}
ACTOR ArgQuadDamage : ArgPowerup
{
	Inventory.PickupMessage "$ARG_GOT_QUAD"
	States
	{
	Spawn:
		QUAD ABCDEFEDCB 3 Bright
		Loop
	Pickup:
		TNT1 A 0 A_PlaySound("items/powerups/quad", CHAN_7)
		TNT1 A 0 A_GiveInventory("ArgPowerQuadDamage")
		Stop
	}
}

// -- Haste --

ACTOR ArgPowerHasteSpeed : PowerSpeed
{
	Powerup.Duration -45
	Powerup.Color "FFCC00" 0.25
	Inventory.Icon "ICONHSTE"
	Speed 1.5
}
ACTOR ArgPowerHasteFiringSpeed : PowerDoubleFiringSpeed
{
	// [XA] at the moment this doesn't really do much since the weapon
	// animations use 1-tic animations everywhere. I'll have to come up
	// with something more clever at some point.
	Powerup.Duration -45
}
ACTOR ArgHaste : ArgPowerup
{
	Inventory.PickupMessage "$ARG_GOT_HASTE"
	States
	{
	Spawn:
		HSTE ABCDEFEDCB 3 Bright
		Loop
	Pickup:
		TNT1 A 0 A_PlaySound("items/powerups/haste", CHAN_7)
		TNT1 A 0 A_GiveInventory("ArgPowerHasteSpeed")
		TNT1 A 0 A_GiveInventory("ArgPowerHasteFiringSpeed")
		Stop
	}
}

// -- Regen --

ACTOR ArgPowerRegen : PowerRegeneration
{
	Powerup.Duration -60
	Powerup.Strength 2
	Powerup.Color "0080FF" 0.25
	Inventory.Icon "ICONREGN"
}
ACTOR ArgRegen : ArgPowerup
{
	Inventory.PickupMessage "$ARG_GOT_REGEN"
	States
	{
	Spawn:
		REGN ABCDEFEDCB 3 Bright
		Loop
	Pickup:
		TNT1 A 0 A_PlaySound("items/powerups/regen", CHAN_7)
		TNT1 A 0 A_GiveInventory("ArgPowerRegen")
		Stop
	}
}

// -- Classic powerups w/cvar-controlled spawners --

ACTOR ArgBlurSphere : BlurSphere replaces BlurSphere
{
	Inventory.PickupSound "items/powerup"
	States
	{
	Spawn:
		PINS A 0 NoDelay A_JumpIf(GetCvar("arg_powerupquad"), "AltItem")
		Goto Super::Spawn
	AltItem:
		PINS A 0 A_SpawnItemEx("ArgQuadDamage", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION, 0, tid)
		PINS A 0 Thing_Remove (0)
		Stop
	}
}

ACTOR ArgInfrared : Infrared replaces Infrared
{
	Inventory.PickupSound "items/automap"
	States
	{
	Spawn:
		PVIS A 0 NoDelay A_JumpIf(GetCvar("arg_poweruphaste"), "AltItem")
		Goto Super::Spawn
	AltItem:
		PVIS A 0 A_SpawnItemEx("ArgHaste", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION, 0, tid)
		PVIS A 0 Thing_Remove (0)
		Stop
	}
}

ACTOR ArgSoulsphere : Soulsphere replaces Soulsphere
{
    Inventory.MaxAmount 0
	Inventory.PickupSound "items/megahealth"
	States
	{
	Spawn:
		SOUL A 0 NoDelay A_JumpIf(GetCvar("arg_powerupregen"), "AltItem")
		Goto Super::Spawn
	AltItem:
		SOUL A 0 A_SpawnItemEx("ArgRegen", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION, 0, tid)
		SOUL A 0 Thing_Remove (0)
		Stop
	}
}

// -- Misc. edits, for sound's sake --

ACTOR ArgAutomap : Allmap replaces Allmap
{
	Inventory.PickupSound "items/automap"
}
ACTOR ArgRadsuit : Radsuit replaces Radsuit
{
	Inventory.PickupSound "items/powerup"
}

// --------------------------------------------------------------------------
//
// ARGENT ORANGE: Additional Powerups
//
// --------------------------------------------------------------------------

// -- Argent Cell --

ACTOR ArgArgentCell : ArgPowerup replaces Berserk
{
	Inventory.PickupMessage "$ARG_GOT_ARGENTCELL"
	Inventory.PickupSound "items/powerup"
    
	States
	{
	Spawn:
		ARGN ABCD 6 Bright
		Loop
	Pickup:
		TNT1 A 0 ACS_NamedExecuteWithResult("Orange_ArgentCell")
		Stop
	}
}

ACTOR ArgModPack : CustomInventory replaces Backpack
{
    Inventory.PickupMessage "A backpack!"
    Inventory.PickupSound "items/weapon"
    
    States
    {
    Spawn:
        BPAK A -1
        Stop
    Pickup:
        TNT1 A 0 {
            if (CountInv ("ArgShotgun") && (!CountInv ("ArgShotgunMod1Enabled") || !CountInv ("ArgShotgunMod2Enabled")))
            {
                if (!CountInv ("ArgShotgunMod1Enabled"))
                {
                    A_GiveInventory ("ArgShotgunMod1Enabled", 1);
                    A_Log ("Acquired Charged Burst!");
                    return state("Done");
                }
                else if (!CountInv ("ArgShotgunMod2Enabled"))
                {
                    A_GiveInventory ("ArgShotgunMod2Enabled", 1);
                    A_Log ("Acquired Explosive Shot!");
                    return state("Done");
                }
            }

            if (CountInv ("ArgHeavyAR") && (!CountInv ("ArgHeavyARMod1Enabled") || !CountInv ("ArgHeavyARMod2Enabled")))
            {
                if (!CountInv ("ArgHeavyARMod1Enabled"))
                {
                    A_GiveInventory ("ArgHeavyARMod1Enabled", 1);
                    A_Log ("Acquired Micro Missiles!");
                    return state("Done");
                }
                else if (!CountInv ("ArgHeavyARMod2Enabled"))
                {
                    A_GiveInventory ("ArgHeavyARMod2Enabled", 1);
                    A_Log ("Acquired Tactical Scope!");
                    return state("Done");
                }
            }

            if (CountInv ("ArgChaingun") && (!CountInv ("ArgChaingunMod1Enabled") || !CountInv ("ArgChaingunMod2Enabled")))
            {
                if (!CountInv ("ArgChaingunMod1Enabled"))
                {
                    A_GiveInventory ("ArgChaingunMod1Enabled", 1);
                    A_Log ("Acquired Gatling Rotator!");
                    return state("Done");
                }
                else if (!CountInv ("ArgChaingunMod2Enabled"))
                {
                    A_GiveInventory ("ArgChaingunMod2Enabled", 1);
                    A_Log ("Acquired Mobile Turret!");
                    return state("Done");
                }
            }

/*
            if (CountInv ("ArgRocketLauncher") && (!CountInv ("ArgRocketLauncherMod1Enabled") || !CountInv ("ArgRocketLauncherMod2Enabled")))
            {
                if (!CountInv ("ArgRocketLauncherMod1Enabled"))
                {
                    A_GiveInventory ("ArgRocketLauncherMod1Enabled", 1);
                    A_Log ("Acquired Remote Detonator!");
                    return state("Done");
                }
                else if (!CountInv ("ArgRocketLauncherMod2Enabled"))
                {
                    A_GiveInventory ("ArgRocketLauncherMod2Enabled", 1);
                    A_Log ("Acquired Lock-on Burst!");
                    return state("Done");
                }
            }
*/

            if (CountInv ("ArgPlasmaRifle") && (!CountInv ("ArgPlasmaRifleMod1Enabled") || !CountInv ("ArgPlasmaRifleMod2Enabled")))
            {
                if (!CountInv ("ArgPlasmaRifleMod1Enabled"))
                {
                    A_GiveInventory ("ArgPlasmaRifleMod1Enabled", 1);
                    A_Log ("Acquired Heat Blast!");
                    return state("Done");
                }
                else if (!CountInv ("ArgPlasmaRifleMod2Enabled"))
                {
                    A_GiveInventory ("ArgPlasmaRifleMod2Enabled", 1);
                    A_Log ("Acquired Stun Bomb!");
                    return state("Done");
                }
            }

/*
            if (CountInv ("ArgGaussCannon") && (!CountInv ("ArgGaussCannonMod1Enabled") || !CountInv ("ArgGaussCannonMod2Enabled")))
            {
                if (!CountInv ("ArgGaussCannonMod1Enabled"))
                {
                    A_GiveInventory ("ArgGaussCannonMod1Enabled", 1);
                    A_Log ("Acquired Precision Scope!");
                    return state("Done");
                }
                else if (!CountInv ("ArgGaussCannonMod2Enabled"))
                {
                    A_GiveInventory ("ArgGaussCannonMod2Enabled", 1);
                    A_Log ("Acquired Siege Mode!");
                    return state("Done");
                }
            }
*/
            return state("HaveAllMods");
        }
                
    HaveAllMods:
        TNT1 A 0
        Fail
    
    Done:
        TNT1 A 0 A_ChangeFlag ("ACTLIKEBRIDGE", 0)
        Stop
    }
}
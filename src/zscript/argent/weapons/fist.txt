// --------------------------------------------------------------------------
//
// ARGENT: Fist
//
// --------------------------------------------------------------------------

class ArgFist : ArgWeapon replaces Fist
{
	Default
	{
		Weapon.UpSound "";
		Weapon.SelectionOrder 10;
		Inventory.Icon "graphics/hud/weapon-fist.png";
		Inventory.PickupMessage "$ARG_GOT_FIST";
		Obituary "$ARG_OB_FIST";
		Tag "$ARG_TAG_FIST";
		DamageType "ArgFist";
	
		+WEAPON.WIMPY_WEAPON
		+WEAPON.MELEEWEAPON
	}
	States
	{
	Spawn:
		PUNG A -1;
		Stop;
	Ready:
		PUNG A 1 A_WeaponReady();
		Loop;
	Select:
		PUNG A 0;
		Goto SelectLoop;
	Deselect:
		PUNG A 0;
		Goto DeselectLoop;
	Fire:
		PUNG A 1   A_WeaponOffset(  8.0, 40.0);
		PUNG A 1   A_WeaponOffset( 16.0, 48.0);
		PUNG A 1   A_WeaponOffset( 24.0, 64.0);
		TNT1 A 1   A_WeaponOffset( 32.0, 80.0);
		TNT1 A 1   A_WeaponOffset(-80.0, 80.0);
	Hold:
		PUNG B 1 { A_WeaponOffset(-64.0, 64.0); A_PlaySound("weapons/fist/swing", CHAN_6); }
		PUNG B 1   A_WeaponOffset(-48.0, 48.0);
		PUNG C 1   A_WeaponOffset(-24.0, 24.0);
		PUNG C 1   A_WeaponOffset(- 0.0,  0.0);
		PUNG C 1 {
			A_WeaponOffset( 10.0,-10.0);
			A_CustomPunch(30 + (2 * random(0, 10)), true, 0, "ArgFistPuff", 96, 0, 0, "None", "weapons/fist/hit");
		}
		PUNG D 1   A_WeaponOffset( 15.0,-15.0);
		PUNG D 1   A_WeaponOffset( 16.0,-16.0);
		PUNG D 1   A_WeaponOffset( 15.0,-15.0);
		PUNG D 1   A_WeaponOffset( 12.0,-12.0);
		PUNG D 1   A_WeaponOffset(  8.0,- 8.0);
		PUNG C 1   A_WeaponOffset(  0.0,  0.0);
		PUNG C 1   A_WeaponOffset(-16.0, 16.0);
		PUNG C 1   A_WeaponOffset(-32.0, 32.0);
		PUNG B 1   A_WeaponOffset(-48.0, 48.0);
	FireEnd:
		TNT1 A 1 { A_WeaponOffset(-80.0, 80.0); A_Refire(); }
		TNT1 A 1   A_WeaponOffset( 32.0, 80.0);
		PUNG A 1   A_WeaponOffset( 24.0, 64.0);
		PUNG A 1   A_WeaponOffset( 16.0, 48.0);
		PUNG A 1   A_WeaponOffset(  8.0, 40.0);
		Goto Ready;
	}
}

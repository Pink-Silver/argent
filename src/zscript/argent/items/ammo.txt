// --------------------------------------------------------------------------
//
// ARGENT: Ammo
//
// --------------------------------------------------------------------------

class ArgClip       : Clip       replaces Clip       { Default { Inventory.PickupSound "items/ammo"; Scale 0.66; } }
class ArgClipBox    : ClipBox    replaces ClipBox    { Default { Inventory.PickupSound "items/ammo"; Scale 0.66; } }
class ArgShell      : Shell      replaces Shell      { Default { Inventory.PickupSound "items/ammo"; Scale 0.66; } }
class ArgShellBox   : ShellBox   replaces ShellBox   { Default { Inventory.PickupSound "items/ammo"; Scale 0.66; } }
class ArgCell       : Cell       replaces Cell       { Default { Inventory.PickupSound "items/ammo"; Scale 0.75; } }
class ArgCellPack   : CellPack                       { Default { Inventory.PickupSound "items/ammo"; Scale 0.75; } }

class ArgRocketAmmo : Ammo replaces RocketAmmo
{
	Default
	{
		Inventory.PickupSound "items/ammo";
		Inventory.PickupMessage "$GOTROCKET";
		Inventory.Amount 1;
		Inventory.MaxAmount 30;
		Ammo.BackpackAmount 1;
		Ammo.BackpackMaxAmount 60;
		Inventory.Icon "ROCKA0";
		Scale 0.66;
	}
	States
	{
	Spawn:
		ROCK A -1;
		Stop;
	}
}

class ArgRocketBox : ArgRocketAmmo replaces RocketBox
{
	Default
	{
		Inventory.PickupMessage "$GOTROCKBOX";
		Inventory.Amount 5;
		Scale 0.5;
	}
	States
	{
	Spawn:
		BROK A -1;
		Stop;
	}
}

class ArgBFGAmmo : Ammo
{
	Default
	{
		Inventory.PickupSound "items/ammo";
		Inventory.PickupMessage "$ARG_GOT_BFGCELL";
		Inventory.Icon "BFGCA0";
		Inventory.Amount 1;
		Inventory.MaxAmount 5;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 10;
		Scale 0.66;

		+FLOATBOB
	}
	States
	{
	Spawn:
		BFGC BC 5 Bright;
		Loop;
	}
}

class ArgBFGAmmoPickup : CustomInventory
{
	Default
	{
		Inventory.PickupSound "items/ammo";
		Inventory.PickupMessage "$ARG_GOT_BFGCELL";
		Scale 0.66;

		+FLOATBOB
	}
	States
	{
	Spawn:
		BFGC BC 5 Bright ;
		Loop;
	Pickup:
		TNT1 A 0 A_GiveInventory("ArgBFGAmmo", 1);
		TNT1 A 0 A_GiveInventory("Cell"      ,60);
		Stop;
	}
}

class ArgBFGAmmoSpawner : Actor replaces CellPack
{
	Default
	{
		Scale 0.66;
		+FORCEYBILLBOARD;
	}
	States
	{
	Spawn:
		BFGC A 0 NoDelay {
			// spawn a cellpack and destroy oneself if the BFG Ammotype CVAR
			// isn't enabled.
			if(GetCvar("arg_bfgammotype") == false) {
				A_SpawnItemEx("ArgCellPack",0,0,0,0,0,0,0,SXF_TRANSFERSPECIAL);
				return ResolveState("Vanish");
			} else {
				A_SpawnItemEx("ArgBFGAmmoPickup",0,0,0,0,0,0,0,SXF_ISTRACER|SXF_TRANSFERSPECIAL);
			}
			return ResolveState(null);
		}
	Idle:
		BFGC A 1;
		BFGC A 1 {
			if(IsPointerEqual(AAPTR_TRACER, AAPTR_NULL)) {
				return ResolveState("Vanish");
			}
			return ResolveState(null);
		}
		Wait;
	Vanish:
		TNT1 A 0;
		Stop;
	}
}

// --------------------------------------------------------------------------
//
// ARGENT: Super Shotgun
//
// --------------------------------------------------------------------------

ACTOR ArgSuperShotgun : ArgWeapon replaces SuperShotgun
{
	Weapon.SelectionOrder 1
	Weapon.AmmoUse 2
	Weapon.AmmoUse  2
	Weapon.AmmoUse2 2
	Weapon.AmmoGive 8
	Weapon.AmmoType  "Shell"
	Weapon.AmmoType2 "Shell"
	Inventory.Icon "graphics/hud/weapon-ssg.png"
	Inventory.PickupMessage "$ARG_GOT_SSG"
	Tag "$ARG_TAG_SSG"
	Obituary "$ARG_OB_SSG"
	DamageType "ArgSuperShotgun"

	States
	{
	Spawn:
		SSGP A -1
		Stop
	Ready:
		SSGG A 1 A_WeaponReady
		Loop
	Select:
		SSGG A 0
		Goto SelectLoop
	Deselect:
		SSGG A 0 A_ZoomFactor(1.0)
		Goto DeselectLoop
	Fire:
		SSGF A 2 Bright {
			A_GunFlash;
			A_AlertMonsters;
			A_FireBullets(11.2, 7.1, 20, 5, "ArgBulletPuff");
			A_PlaySound("weapons/ssg/fire", CHAN_5);
		}
	FireAnim:
		SSGF BCDEF 1
		SSGF F 1 A_JumpIfNoAmmo("ImOutOfAmmo")
		SSGF EGHIJKLMNOPQRSTUVXYZ 1
		SSGG BCDEFGHIJKLMNOPQRSTUVWX 1
		SSGG A 0 A_Refire
		Goto Ready
	ImOutOfAmmo:
		SSGF FEDCB 1
		SSGG A 1
		SSGG A 0 A_CheckReload
		Goto Ready
	Flash:
		TNT1 A 4 Bright A_Light2
		TNT1 A 3 Bright A_Light1
		Goto LightDone
	AltFire:
		SSGA A 1 {
			A_PlaySound("weapons/ssg/aimup", CHAN_AUTO);
			A_ZoomFactor(1.25);
			A_WeaponOffset(-2.5, 32.0);
		}
		SSGA A 1 A_WeaponOffset(- 5.0, 32.0)
		SSGA B 1 A_WeaponOffset(-10.0, 32.0)
		SSGA B 1 A_WeaponOffset(-12.0, 31.0)
		SSGA B 5 A_WeaponOffset(-13.0, 30.0)
	AltHoldIsh:
		SSGA B 1 A_JumpIf(GetPlayerInput(INPUT_BUTTONS) & BT_ATTACK, "AltShoot")
		SSGA B 0 A_Refire("AltHoldIsh")
	AltDown:
		SSGA B 1 {
			A_PlaySound("weapons/ssg/aimdown", CHAN_AUTO);
			A_ZoomFactor(1.0);
			A_WeaponOffset(-12.0, 31.0);
		}
		SSGA B 1 A_WeaponOffset(-10.0, 32.0)
		SSGA A 1 A_WeaponOffset(- 5.0, 32.0)
		SSGA A 1 A_WeaponOffset(- 2.2, 32.0)
		Goto Ready
	AltShoot:
		SSGB A 2 Bright {
			A_GunFlash;
			A_FireBullets(6.0, 3.0, 20, 5, "ArgBulletPuff");
			A_PlaySound("weapons/ssg/fire", CHAN_5);
			A_ZoomFactor(1.0);
		}
		SSGB A 0 A_WeaponOffset(0.0, 32.0)
		Goto FireAnim
	}
}

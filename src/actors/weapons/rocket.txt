// --------------------------------------------------------------------------
//
// ARGENT: Rocket Launcher
//
// --------------------------------------------------------------------------

ACTOR ArgRocketLauncher : ArgModWeapon replaces RocketLauncher
{
	Weapon.SelectionOrder 7
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.AmmoType "RocketAmmo"
	Inventory.Icon "graphics/hud/weapon-rocket.png"
	Inventory.PickupMessage "$ARG_GOT_ROCKET"
	Tag "$ARG_TAG_ROCKET"

	+WEAPON.NOAUTOFIRE

	States
	{
	Spawn:
		RKLP A -1
		Stop
	Ready:
		RKLG A 0
		Goto Super::Ready
	Select:
		RKLG A 0
		Goto SelectLoop
	Deselect:
		RKLG A 0 A_StopSound(CHAN_5)
		Goto DeselectLoop

	/* Unmodded + Primary */

	Mod0Select:
		RKLG A 0
		Goto SelectLoop
	Mod0Swap:
		RKLG A 0
		Goto ModSwapUp
	Mod0Ready:
		RKLG A 0
		Goto ReadyLoop
	Mod0Fire:
		RKLG B 1 Bright {
			A_GunFlash;
			A_FireCustomMissile("ArgRocket", 0, 1, 4.0 + frandom(-0.50, 0.50), frandom(-0.50, 0.50));
			A_PlaySound("weapons/rocket/fire", CHAN_5);
			A_AlertMonsters;
			A_WeaponOffset(6.0, 3.0 + 32);
		}
		RKLG C 1 A_WeaponOffset(16.0,  8.0 + 32)
		RKLG D 1 A_WeaponOffset(20.0, 10.0 + 32)
		RKLG E 1 A_WeaponOffset(22.0, 11.0 + 32)
		RKLG F 1 A_WeaponOffset(23.0, 11.5 + 32)
		RKLG G 1 
		RKLG A 1 
		RKLG A 1 A_WeaponOffset(22.0, 11.0 + 32)
		RKLG A 1 A_WeaponOffset(20.0, 10.0 + 32)
		RKLG H 1 A_WeaponOffset(17.0,  8.5 + 32)
		RKLG H 1 A_WeaponOffset(13.0,  6.5 + 32)
		RKLG I 1 A_WeaponOffset(10.0,  5.0 + 32)
		RKLG J 1 A_WeaponOffset( 7.0,  3.5 + 32)
		RKLG K 1 A_WeaponOffset( 5.0,  2.5 + 32)
		RKLG L 1 A_WeaponOffset( 3.0,  1.5 + 32)
		RKLG M 1 A_WeaponOffset( 2.0,  1.0 + 32)
		RKLG N 1 A_WeaponOffset( 1.0,  0.5 + 32)
		RKLG O 1 A_WeaponOffset( 0.0,  0.0 + 32)
		RKLG P 1 
		RKLG Q 5 A_CheckReload
		RKLG Q 1 A_WeaponOffset(-0.25, 0.25+ 32)
		RKLG R 1 A_WeaponOffset(-0.5,  0.5 + 32)
		RKLG S 1 A_WeaponOffset(-1.5,  1.5 + 32)
		RKLG T 1 A_WeaponOffset(-2.5,  2.5 + 32)
		RKLG U 1 A_WeaponOffset(-3.5,  3.5 + 32)
		RKLG V 4 A_WeaponOffset(-4.0,  4.0 + 32)
		RKLG W 1 A_WeaponOffset(-3.5,  3.5 + 32)
		RKLG X 1 A_WeaponOffset(-2.5,  2.5 + 32)
		RKLG Y 1 A_WeaponOffset(-1.5,  1.5 + 32)
		RKLG Z 1 A_WeaponOffset(-0.5,  0.5 + 32)
		RKLG "[" 1 A_WeaponOffset( 0,  0.0 + 32)
		RKLG A 0 A_Refire
		Goto Ready
	Flash:
		TNT1 A 1 Bright A_Light1
		TNT1 A 2 Bright A_Light2
		TNT1 A 1 Bright A_Light1
		Goto LightDone

	/* Mod 1: Remote Detonator */

	/* Mod 2: Lock-on Burst */

	}
}
ACTOR ArgRocketLauncherModSelected : ArgModToken {}
ACTOR ArgRocketLauncherMod1Enabled : ArgToken    {}
ACTOR ArgRocketLauncherMod2Enabled : ArgToken    {}
ACTOR ArgRocketLauncherModCooldown : ArgToken    { Inventory.MaxAmount 120 }

ACTOR ArgRocket : FastProjectile
{
	Radius 11
	Height 8
	Scale 0.75
	Damage 30
	Speed 30
	DamageType "ArgPlayerRocket"
	DeathSound "weapons/rocket/explode"
	MissileType "ArgRocketTrailSpawner"
	MissileHeight 13
	Decal "Scorch"
	Obituary "$ARG_OB_ROCKET"
	
	+EXTREMEDEATH
	
	States
	{
	Spawn:
		MISL A 1 Bright
		Loop
	Death:
		REXP A 4 Bright {
			A_SetScale(1.0);
			A_Explode(200, 128, 1);
		}
		REXP B 3 Bright
		REXP C 3 Bright A_SetTranslucent(0.7)
		REXP D 2 Bright A_SetTranslucent(0.6)
		REXP E 2 Bright A_SetTranslucent(0.5)
		REXP F 2 Bright A_SetTranslucent(0.4)
		Stop
	}
}

ACTOR ArgRocketTrailSpawner : ArgTrail
{
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 1 NoDelay A_SpawnItemEx("ArgRocketTrail", 0, 0, 0, 0, frandom(-0.33, 0.33), frandom(-0.33, 0.33))
		Stop
	}
}

ACTOR ArgRocketTrail : ArgTrail
{
	RenderStyle Add
	Alpha 0.5
	Scale 0.25
	States
	{
	Spawn:
		RTRL HHABCDEFG 1 Bright
		Stop
	}
}

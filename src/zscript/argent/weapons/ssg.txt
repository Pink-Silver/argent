// --------------------------------------------------------------------------
//
// ARGENT: Super Shotgun
//
// --------------------------------------------------------------------------

class ArgSuperShotgun : ArgWeapon replaces SuperShotgun
{
	Default
	{
		Weapon.SelectionOrder 1;
		Weapon.AmmoUse 2;
		Weapon.AmmoUse2 2;
		Weapon.AmmoGive 8;
		Weapon.AmmoType "Shell";
		Weapon.AmmoType2 "Shell";
		Inventory.Icon "graphics/hud/weapon-ssg.png";
		Inventory.PickupMessage "$ARG_GOT_SSG";
		Tag "$ARG_TAG_SSG";
		Obituary "$ARG_OB_SSG";
		DamageType "ArgSuperShotgun";
	}
	States
	{
	Spawn:
		SSGP A -1;
		Stop;
	Ready:
		SSGG A 1 A_WeaponReady();
		Loop;
	Select:
		SSGG A 0;
		Goto SelectLoop;
	Deselect:
		SSGG A 0 A_ZoomFactor(1.0);
		Goto DeselectLoop;
	Fire:
		SSGF A 2 Bright Arg_A_FireSSG();
	FireAnim:
		SSGF BCDEF 1;
		SSGF F 1 A_JumpIfNoAmmo("ImOutOfAmmo");
		SSGF EGHIJKLMNOPQRSTUVXYZ 1;
		SSGG BCDEFGHIJKLMNOPQRSTUVWX 1;
		SSGG A 0 A_Refire();
		Goto Ready;
	ImOutOfAmmo:
		SSGF FEDCB 1;
		SSGG A 1;
		SSGG A 0 A_CheckReload();
		Goto Ready;
	Flash:
		TNT1 A 4 Bright A_Light2();
		TNT1 A 3 Bright A_Light1();
		Goto LightDone;
	AltFire:
		SSGA A 1 {
			A_PlaySound("weapons/ssg/aimup", CHAN_AUTO);
			A_ZoomFactor(1.25);
			Arg_A_WeaponOffset(-2.5, 0);
		}
		SSGA A 1 Arg_A_WeaponOffset(- 5.0, 0);
		SSGA B 1 Arg_A_WeaponOffset(-10.0, 0);
		SSGA B 1 Arg_A_WeaponOffset(-12.0,-1);
		SSGA B 5 Arg_A_WeaponOffset(-13.0,-2);
	AltHoldIsh:
		SSGA B 1 Arg_A_SynthFire("AltShoot");
		SSGA B 0 A_Refire("AltHoldIsh");
	AltDown:
		SSGA B 1 {
			A_PlaySound("weapons/ssg/aimdown", CHAN_AUTO);
			A_ZoomFactor(1.0);
			Arg_A_WeaponOffset(-12.0, -1);
		}
		SSGA B 1 Arg_A_WeaponOffset(-10.0, 0);
		SSGA A 1 Arg_A_WeaponOffset(- 5.0, 0);
		SSGA A 1 Arg_A_WeaponOffset(- 2.2, 0);
		Goto Ready;
	AltShoot:
		SSGB A 2 Bright Arg_A_FireSSG(6.0, 3.0);
		SSGB A 0 Arg_A_WeaponOffset(0, 0);
		Goto FireAnim;
	}
	
	/*
	 * ARGENT SSG fire, with easily-tweakable choke. Because ""realism"".
	 */
	action void Arg_A_FireSSG(float spread_horz = 11.2, float spread_vert = 7.1) {
		A_ZoomFactor(1.0);
		A_FireBullets(spread_horz, spread_vert, 20, 5, "ArgBulletPuff");
		Arg_A_GunFlash('Flash', (CountInv(invoker.AmmoType1) > 0) ? "weapons/ssg/fire" : "weapons/ssg/firelast");
	}
}

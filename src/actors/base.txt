// --------------------------------------------------------------------------
//
// ARGENT: Basic Actors
//
// --------------------------------------------------------------------------

ACTOR ArgTrail
{
	-RANDOMIZE
	+NOBLOCKMONST
	+NOTELEPORT
	+FORCEXYBILLBOARD
	+NOBLOCKMAP
	+NOGRAVITY
	+THRUACTORS
}

ACTOR ArgTrailNoPhysics
{
	+NOINTERACTION
}

ACTOR ArgDrop : ArgTrail
{
	Projectile
	-NOGRAVITY
	Damage 0
	Speed 0
}

ACTOR ArgPuff
{
	+NOGRAVITY
	+PUFFGETSOWNER
	+PUFFONACTORS
}

ACTOR ArgToken : Inventory
{
	+INVENTORY.UNDROPPABLE
}

ACTOR ArgNothing
{
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0
		Stop
	}
}

ACTOR ArgRocketBase : FastProjectile // so Cyber & Player rockets can share some stuff.
{
	Radius 11
	Height 8
	Speed 20
	Damage 20
	Scale 0.75
	DeathSound "weapons/rocket/explode"
	MissileType "ArgRocketTrailSpawner"
	MissileHeight 13
	Decal "Scorch"
	Obituary "$ARG_OB_ROCKET"
	
	+EXTREMEDEATH
	
	States
	{
	Spawn:
		MISL A 1 Bright
		Loop
	Death:
		REXP A 4 Bright {
			A_SetScale(1.0);
			A_Explode(200, 128, 1);
		}
	Boom:
		REXP B 3 Bright
		REXP C 3 Bright A_SetTranslucent(0.7)
		REXP D 2 Bright A_SetTranslucent(0.6)
		REXP E 2 Bright A_SetTranslucent(0.5)
		REXP F 2 Bright A_SetTranslucent(0.4)
		Stop
	}
}

ACTOR ArgRocketTrailSpawner : ArgTrail
{
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 1 NoDelay A_SpawnItemEx("ArgRocketTrail", 0, 0, 0, 0, frandom(-0.33, 0.33), frandom(-0.33, 0.33))
		Stop
	}
}

ACTOR ArgRocketTrail : ArgTrail
{
	RenderStyle Add
	Alpha 0.5
	Scale 0.25
	States
	{
	Spawn:
		RTRL HHABCDEFG 1 Bright
		Stop
	}
}

// --------------------------------------------------------------------------
//
// ARGENT: Plasma Rifle
//
// --------------------------------------------------------------------------

ACTOR ArgPlasmaRifle : ArgModWeapon replaces PlasmaRifle
{
	Weapon.SelectionOrder 4
	Weapon.AmmoUse 1
	Weapon.AmmoGive 40
	Weapon.AmmoType "Cell"
	const int Mod1AmmoUse = 0; // pointless, but may as well be explicit. shit.
	const int Mod2AmmoUse = 10;
	Inventory.Icon "graphics/hud/weapon-plasma.png"
	Inventory.PickupMessage "$ARG_GOT_PLASMA"
	Tag "$ARG_TAG_PLASMA"
	
	States
	{
	Spawn:
		PLSP A -1
		Stop
	Ready:
		PLSG A 0
		Goto Super::Ready

	/* Unmodded + Primary */

	Mod0Select:
		PLSG A 0
		Goto SelectLoop
	Mod0Ready:
		PLSG A 0
		Goto ReadyLoop
	Mod0Fire:
		"####" B 1 Bright {
			A_GunFlash;
			A_AlertMonsters;
			A_FireCustomMissile("ArgPlasma", 0, 1, 6.0 + frandom(-2.00, 2.00), frandom(-2.00, 2.00));
			A_WeaponOffset(0.0, 32.0);
			if(CountInv("ArgPlasmaRifleModSelected") == 1) {
				A_GiveInventory("ArgPlasmaRifleModCharge", 2);
				A_PlaySound("weapons/plasma/altfire", CHAN_5);
				return state("Mod1FirePrimary");
			} else {
				A_PlaySound("weapons/plasma/fire", CHAN_5);
			}
			return state(0);
		}
		"####" C 1 Bright A_WeaponOffset(2.0 + frandom(-0.25, 1.25), 33.5 + frandom(-0.25, 0.25))
		"####" A 1        A_WeaponOffset(1.0 + frandom(-0.25, 2.25), 33.0 + frandom(-0.25, 0.50))
		Goto Ready
	Flash:
		TNT1 A 1 Bright A_Light2
		TNT1 A 1 Bright A_Light1
		Goto LightDone

	/* Mod 1: Heat Blast */
		
	Mod1Select:
		PL1G A 0
		Goto SelectLoop
	Mod1Raise:
		PL1S A 0
		Goto ModSwapUp
	Mod1Swap:
		PL1G A 0 A_PlaySound("weapons/plasma/mod1swap", CHAN_AUTO)
	Mod1Ready:
		PL1G A 0 {
			if(CountInv("ArgPlasmaRifleModCharge") >= 120) { return state("Mod1ReadyD"); }
			if(CountInv("ArgPlasmaRifleModCharge") >=  90) { return state("Mod1ReadyC"); }
			if(CountInv("ArgPlasmaRifleModCharge") >=  60) { return state("Mod1ReadyB"); }
			if(CountInv("ArgPlasmaRifleModCharge") >=  30) { return state("Mod1ReadyA"); }
			return state(0);
		}
		Goto ReadyLoop
	Mod1ReadyA:
		PL1A FFFGGG 1 A_WeaponReady(WRF_ALLOWRELOAD)
		PL1G AAA 1 A_WeaponReady(WRF_ALLOWRELOAD)
		PL1A GGGFFF 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Mod1ReadyB:
		PL1A EEEFFFGGGFFFEEE 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Mod1ReadyC:
		PL1A DDDEEEFFFEEEDDD 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Mod1ReadyD:
		PL1G DDDEEEFFFGGGHHH 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Mod1FirePrimary:
		PL1G B 1 Bright
		PL1G C 1 Bright A_WeaponOffset(2.0 + frandom(-0.25, 1.25), 33.5 + frandom(-0.25, 0.25))
		PL1A D 1        A_WeaponOffset(1.0 + frandom(-0.25, 2.25), 33.0 + frandom(-0.25, 0.50))
		Goto Mod1Ready
	Mod1Up:
		PL1G A 1 A_JumpIfInventory("ArgPlasmaRifleModCharge", 30, "Mod1Hold")
		Goto Mod1Ready
	Mod1Hold:
		PL1A A 1 Bright {
			A_GunFlash;
			A_AlertMonsters;
			A_PlaySound("weapons/plasma/mod1fire", CHAN_6);
			A_Explode(CountInv("ArgPlasmaRifleModCharge") * 3, 160, XF_NOTMISSILE, 0, 128);
			A_Blast(BF_NOIMPACTDAMAGE, CountInv("ArgPlasmaRifleModCharge") * 2, 160, 20, "ArgNothing", "");
			A_TakeInventory("ArgPlasmaRifleModCharge", 999);
			A_Quake(2, 8, 0, 8, "");
			// [TODO] cool heatwave effect
		}
		PL1A B 1 A_WeaponOffset( 5.0,  5.0 + 32.0)
		PL1A B 1 A_WeaponOffset(12.0, 12.0 + 32.0)
		PL1A C 1 A_WeaponOffset(15.0, 15.0 + 32.0)
		PL1A C 1 A_WeaponOffset(16.0, 16.0 + 32.0)
		PL1A D 1 A_WeaponOffset(14.0, 14.0 + 32.0)
		PL1A D 1 A_WeaponOffset(11.0, 11.0 + 32.0)
		PL1A E 1 A_WeaponOffset( 8.0,  8.0 + 32.0)
		PL1A E 1 A_WeaponOffset( 4.0,  4.0 + 32.0)
		PL1A F 1 A_WeaponOffset( 1.0,  1.0 + 33.0)
		PL1A G 1 A_WeaponOffset( 0.0,  0.0 + 32.0)
		Goto Mod1Ready

	/* Mod 2: Stun Bomb */
		
	Mod2Select:
		PL2G A 0
		Goto SelectLoop
	Mod2Raise:
		PL2S A 0
		Goto ModSwapUp
	Mod2Swap:
		PL2G A 0 A_PlaySound("weapons/plasma/mod2swap", CHAN_AUTO)
	Mod2Ready:
		PL2G A 0
		Goto ReadyLoop
	Mod2Up:
		PL2G A 1 A_JumpIfInventory("Cell", 10, "Mod2Hold")
		Goto Mod1Ready
	Mod2Hold:
		PL2G B 1 Bright {
			A_GunFlash;
			A_AlertMonsters;
			A_PlaySound("weapons/plasma/mod2fire", CHAN_6);
			A_FireCustomMissile("ArgStunBomb", 0, 0, 0, 0, FPF_AIMATANGLE , 5);
			A_TakeInventory("Cell", Mod2AmmoUse, TIF_NOTAKEINFINITE);
			A_GiveInventory("ArgPlasmaRifleModCooldown", 200);
		}
		PL2G A 1 A_WeaponOffset( 5.0,  5.0 + 32.0)
		PL2G A 1 A_WeaponOffset(12.0, 12.0 + 32.0)
		PL2G A 1 A_WeaponOffset(15.0, 15.0 + 32.0)
		PL2G A 1 A_WeaponOffset(16.0, 16.0 + 32.0)
		PL2G A 1 A_WeaponOffset(14.0, 14.0 + 32.0)
		PL2G A 1 A_WeaponOffset(11.0, 11.0 + 32.0)
		PL2G A 1 A_WeaponOffset( 8.0,  8.0 + 32.0)
		PL2G A 1 A_WeaponOffset( 4.0,  4.0 + 32.0)
		PL2G A 1 A_WeaponOffset( 1.0,  1.0 + 33.0)
		PL2G A 1 A_WeaponOffset( 0.0,  0.0 + 32.0)
		Goto Cooldown
	}
}
ACTOR ArgPlasmaRifleModSelected : ArgModToken {}
ACTOR ArgPlasmaRifleMod1Enabled : ArgToken    {}
ACTOR ArgPlasmaRifleMod2Enabled : ArgToken    {}
ACTOR ArgPlasmaRifleModCooldown : ArgToken    { Inventory.MaxAmount 200 }
ACTOR ArgPlasmaRifleModCharge   : ArgToken    { Inventory.MaxAmount 120 }

ACTOR ArgPlasma : PlasmaBall
{
	Radius 8
	Height 8
	Damage 5
	Scale 0.75
	Speed 50
	SeeSound ""
	DeathSound "weapons/plasma/explode"
	Decal "PlasmaScorchLower"
	DamageType "ArgPlasma"
	Obituary "$ARG_OB_PLASMA"
	States
	{
	Spawn:
		PLSS AB 3 Bright
		Loop
	Death:
		PLSE ABCDE 2 Bright
		Stop
	}
}

ACTOR ArgStunBomb : ArgPlasma
{
	Speed 40
	Damage 0
	DeathSound "weapons/plasma/mod2hit"
	DamageType "ArgStun"
	
	-NOGRAVITY
	
	States {
	Spawn:
		PBOM ABCD 1 Bright
		Loop
	Death:
		PBOM E 3 {
			A_NoGravity;
			A_RadiusGive("ArgStunner", 160, RGF_MONSTERS);
		}
		PBOM FGHI 3 Bright
		Stop
	}
}

ACTOR ArgStunner : CustomInventory
{
	-INVENTORY.INVBAR
	States
	{
	Spawn:
		TNT1 A 0
		Stop
	Pickup:
		TNT1 A 1 ACS_NamedExecuteAlways("NeuralStunner")
		Stop
	}
}

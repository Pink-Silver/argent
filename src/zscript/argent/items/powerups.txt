// --------------------------------------------------------------------------
//
// ARGENT: Powerups
//
// --------------------------------------------------------------------------

ACTOR ArgPowerup : CustomInventory
{
	Inventory.PickupSound "items/powerup"
	Inventory.MaxAmount 0

	+COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.FANCYPICKUPSOUND
}

// -- Berserk --

ACTOR ArgPowerBerserk : PowerDamage
{
    DamageFactor "ArgFist", 10.0
	Inventory.Icon "ICONBZRK"
	Powerup.Duration -30
	Powerup.Colormap 0.0,0.0,0.0, 1.0,0.0,0.0
	
	+INVENTORY.ALWAYSPICKUP
}

ACTOR ArgPowerBerserkInvulnerable : PowerInvulnerable
{
	Powerup.Duration -30
	
	+INVENTORY.ALWAYSPICKUP
}

ACTOR ArgPowerBerserkSpeed : PowerSpeed
{
    Speed 1.5
	Powerup.Duration -30
	
	+INVENTORY.ALWAYSPICKUP
}

ACTOR ArgBerserk : Berserk replaces Berserk
{
	Inventory.PickupMessage "$ARG_GOT_BERSERK"
	Inventory.PickupSound "items/powerup"
	
    +INVENTORY.BIGPOWERUP

	States
	{
	Spawn:
		BZRK ABCDEFEDCB 3 Bright
		Loop
	Pickup:
		TNT1 A 0 A_PlaySound("items/powerups/berserk", CHAN_7)
		TNT1 A 0 A_GiveInventory("ArgPowerBerserk")
		TNT1 A 0 A_GiveInventory("ArgPowerBerserkInvulnerable")
		TNT1 A 0 A_GiveInventory("ArgPowerBerserkSpeed")
		TNT1 A 0 HealThing(100, 0)
		TNT1 A 0 A_SelectWeapon("ArgFist")
		Stop
	}
}

// -- Quad --

ACTOR ArgPowerQuadDamage : PowerDamage
{
	Inventory.Icon "ICONQUAD"
	DamageFactor "Normal", 4
	Powerup.Duration -45
	Powerup.Color "AA00FF" 0.25
	
	+INVENTORY.ALWAYSPICKUP
}

ACTOR ArgQuadDamage : ArgPowerup
{
	Inventory.PickupMessage "$ARG_GOT_QUAD"

    +INVENTORY.BIGPOWERUP

	States
	{
	Spawn:
		QUAD ABCDEFEDCB 3 Bright
		Loop
	Pickup:
		TNT1 A 0 A_PlaySound("items/powerups/quad", CHAN_7)
		TNT1 A 0 A_GiveInventory("ArgPowerQuadDamage")
		Stop
	}
}

// -- Haste --

ACTOR ArgPowerHasteSpeed : PowerSpeed
{
	Powerup.Duration -45
	Powerup.Color "FFCC00" 0.25
	Powerup.Colormap 0.1,0.075,0.0, 1.0,0.8,0.0
	Inventory.Icon "ICONHSTE"
	Speed 1.5
	
	+INVENTORY.ALWAYSPICKUP
}

ACTOR ArgPowerHasteFiringSpeed : PowerDoubleFiringSpeed
{
	// [XA] at the moment this doesn't really do much since the weapon
	// animations use 1-tic animations everywhere. I'll have to come up
	// with something more clever at some point.
	Powerup.Duration -45
	
	+INVENTORY.ALWAYSPICKUP
}

ACTOR ArgHaste : ArgPowerup
{
	Inventory.PickupMessage "$ARG_GOT_HASTE"
	States
	{
	Spawn:
		HSTE ABCDEFEDCB 3 Bright
		Loop
	Pickup:
		TNT1 A 0 A_PlaySound("items/powerups/haste", CHAN_7)
		TNT1 A 0 A_GiveInventory("ArgPowerHasteSpeed")
		TNT1 A 0 A_GiveInventory("ArgPowerHasteFiringSpeed")
		Stop
	}
}

// -- Regen --

ACTOR ArgPowerRegen : PowerRegeneration
{
	Powerup.Duration -60
	Powerup.Strength 2
	Powerup.Color "0080FF" 0.25
	Inventory.Icon "ICONREGN"
	
	+INVENTORY.ALWAYSPICKUP
}

ACTOR ArgRegen : ArgPowerup
{
	Inventory.PickupMessage "$ARG_GOT_REGEN"
	States
	{
	Spawn:
		REGN ABCDEFEDCB 3 Bright
		Loop
	Pickup:
		TNT1 A 0 A_PlaySound("items/powerups/regen", CHAN_7)
		TNT1 A 0 A_GiveInventory("ArgPowerRegen")
		Stop
	}
}

// -- Misc. edits, for sound's sake --

ACTOR ArgSoulsphere : Soulsphere replaces Soulsphere
{
    Inventory.Amount 200
    Inventory.MaxAmount 200
	Inventory.PickupSound "items/megahealth"
}

ACTOR InvulnerabilityEffect : PowerupGiver
{
    Inventory.MaxAmount 0
    Powerup.Type "Invulnerable"
    Powerup.Color InverseMap

    +INVENTORY.AUTOACTIVATE
    +INVENTORY.ALWAYSPICKUP
}

ACTOR ArgInvuln : ArgPowerup replaces InvulnerabilitySphere
{
	Inventory.PickupMessage "$GOTINVUL"

    +INVENTORY.BIGPOWERUP

	States
	{
	Spawn:
		PINV ABCDCB 6 Bright
		Loop
	Pickup:
		TNT1 A 0 A_PlaySound("items/powerups/berserk", CHAN_7)
		TNT1 A 0 A_GiveInventory("InvulnerabilityEffect")
		Stop
	}
}

ACTOR BlurEffect : PowerupGiver
{
    Inventory.MaxAmount 0
    Powerup.Type "Invisibility"

    +INVENTORY.AUTOACTIVATE
    +INVENTORY.ALWAYSPICKUP
}

ACTOR ArgBlurSphere : ArgPowerup replaces BlurSphere
{
    RenderStyle Translucent
	Inventory.PickupMessage "$GOTINVIS"

    +INVENTORY.BIGPOWERUP
    +VISIBILITYPULSE

	States
	{
	Spawn:
		PINS ABCDCB 6 Bright
		Loop
	Pickup:
		TNT1 A 0 A_PlaySound("items/powerups/regen", CHAN_7)
		TNT1 A 0 A_GiveInventory("BlurEffect")
		Stop
	}
}

ACTOR ArgInfrared : Infrared replaces Infrared
{
	Inventory.PickupSound "items/automap"
}
ACTOR ArgAutomap : Allmap replaces Allmap
{
	Inventory.PickupSound "items/automap"
}
ACTOR ArgRadsuit : Radsuit replaces Radsuit
{
	Inventory.PickupSound "items/powerup"
}

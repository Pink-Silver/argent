// --------------------------------------------------------------------------
//
// ARGENT: Rocket Launcher
//
// --------------------------------------------------------------------------

ACTOR ArgRocketLauncher : ArgModWeapon replaces RocketLauncher
{
	Weapon.SelectionOrder 7
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.AmmoType "ArgRocketAmmo"
	Inventory.Icon "graphics/hud/weapon-rocket.png"
	Inventory.PickupMessage "$ARG_GOT_ROCKET"
	Tag "$ARG_TAG_ROCKET"

	+WEAPON.NOAUTOFIRE

	States
	{
	Spawn:
		RKLP A -1
		Stop
	Ready:
		RKLG A 0
		Goto Super::Ready
	Select:
		RKLG A 0
		Goto SelectLoop
	Deselect:
		RKLG A 0
		Goto DeselectLoop

	/* Unmodded + Primary */

	Mod0Select:
		RKLG A 0
		Goto SelectLoop
	Mod0Swap:
		RKLG A 0
		Goto ModSwapUp
	Mod0Ready:
		RKLG A 0
		Goto ReadyLoop
	Mod0Fire:
		RKLF A 2 Bright {
			A_GunFlash;
			A_FireCustomMissile("ArgRocket", 0, 1, 4.0 + frandom(-0.50, 0.50), frandom(-0.50, 0.50));
			A_PlaySound("weapons/rocket/fire", CHAN_5);
		}
		RKLF B 1 A_WeaponOffset( 2.0, 33.5)
		RKLF B 1 A_WeaponOffset( 6.0, 35.5)
		RKLF C 1 A_WeaponOffset(10.0, 38.0)
		RKLF C 1 A_WeaponOffset(16.0, 41.0)
		RKLF D 1 A_WeaponOffset(20.0, 43.0)
		RKLF D 1 A_WeaponOffset(22.0, 44.5)
		RKLF E 1 A_WeaponOffset(23.0, 46.2)
		RKLF E 1 A_WeaponOffset(24.0, 46.5)
		RKLF E 1 A_WeaponOffset(24.5, 46.6)
		RKLF E 1 A_WeaponOffset(23.3, 46.5)
		RKLF D 1 A_WeaponOffset(21.5, 46.2)
		RKLF D 1 A_WeaponOffset(18.0, 45.5)
		RKLF C 1 A_WeaponOffset(13.0, 43.0)
		RKLF C 1 A_WeaponOffset( 9.0, 39.0)
		RKLF B 1 A_WeaponOffset( 6.5, 35.5)
		RKLF B 1 A_WeaponOffset( 2.0, 33.5)
		RKLG A 1 A_WeaponOffset( 0.0, 32.0)
		RKLG A 1 A_CheckReload
		RKLG BCDEFGHIJ 1
		RKLG K 3
		RKLG LMNOP 1
		RKLG A 0 A_Refire
		Goto Ready
	Flash:
		TNT1 A 2 Bright A_Light2
		TNT1 A 1 Bright A_Light1
		Goto LightDone

	/* Mod 1: Remote Detonator */

	/* Mod 2: Lock-on Burst */

	}
}
ACTOR ArgRocketLauncherModSelected : ArgModToken {}
ACTOR ArgRocketLauncherMod1Enabled : ArgToken    {}
ACTOR ArgRocketLauncherMod2Enabled : ArgToken    {}
ACTOR ArgRocketLauncherModCooldown : ArgToken    { Inventory.MaxAmount 120 }

ACTOR ArgRocket : FastProjectile
{
	Radius 11
	Height 8
	Scale 0.75
	Damage 30
	Speed 30
	DamageType "ArgPlayerRocket"
	DeathSound "weapons/rocket/explode"
	MissileType "ArgRocketTrailSpawner"
	MissileHeight 13
	Decal "Scorch"
	Obituary "$ARG_OB_ROCKET"
	
	+EXTREMEDEATH
	
	States
	{
	Spawn:
		MISL A 1 Bright
		Loop
	Death:
		REXP A 4 Bright {
			A_SetScale(1.0);
			A_Explode(200, 128, 1);
		}
		REXP B 3 Bright
		REXP C 3 Bright A_SetTranslucent(0.7)
		REXP D 2 Bright A_SetTranslucent(0.6)
		REXP E 2 Bright A_SetTranslucent(0.5)
		REXP F 2 Bright A_SetTranslucent(0.4)
		Stop
	}
}

ACTOR ArgRocketTrailSpawner : ArgTrail
{
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 1 NoDelay A_SpawnItemEx("ArgRocketTrail", 0, 0, 0, 0, frandom(-0.33, 0.33), frandom(-0.33, 0.33))
		Stop
	}
}

ACTOR ArgRocketTrail : ArgTrail
{
	RenderStyle Add
	Alpha 0.5
	Scale 0.25
	States
	{
	Spawn:
		RTRL HHABCDEFG 1 Bright
		Stop
	}
}

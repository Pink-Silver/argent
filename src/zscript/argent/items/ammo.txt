// --------------------------------------------------------------------------
//
// ARGENT: Ammo
//
// --------------------------------------------------------------------------

ACTOR ArgClip       : Clip       replaces Clip       { Inventory.PickupSound "items/ammo" Scale 0.66 }
ACTOR ArgClipBox    : ClipBox    replaces ClipBox    { Inventory.PickupSound "items/ammo" Scale 0.66 }
ACTOR ArgShell      : Shell      replaces Shell      { Inventory.PickupSound "items/ammo" Scale 0.66 }
ACTOR ArgShellBox   : ShellBox   replaces ShellBox   { Inventory.PickupSound "items/ammo" Scale 0.66 }
ACTOR ArgCell       : Cell       replaces Cell       { Inventory.PickupSound "items/ammo" Scale 0.75 }
ACTOR ArgCellPack   : CellPack                       { Inventory.PickupSound "items/ammo" Scale 0.75 }

ACTOR ArgRocketAmmo : Ammo replaces RocketAmmo
{
	Inventory.PickupSound "items/ammo"
	Inventory.PickupMessage "$GOTROCKET"
	Inventory.Amount 1
	Inventory.MaxAmount 30
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 60
	Inventory.Icon "ROCKA0"
	Scale 0.66
	States
	{
	Spawn:
		ROCK A -1
		Stop
	}
}

ACTOR ArgRocketBox : ArgRocketAmmo replaces RocketBox
{
	Inventory.PickupMessage "$GOTROCKBOX"
	Inventory.Amount 5
	Scale 0.5
	States
	{
	Spawn:
		BROK A -1
		Stop
	}
}

ACTOR ArgBFGAmmo : Ammo
{
	Inventory.PickupSound "items/ammo"
	Inventory.PickupMessage "$ARG_GOT_BFGCELL"
	Inventory.Icon BFGCA0
	Inventory.Amount 1
	Inventory.MaxAmount 5
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 10
	Scale 0.66
	+FLOATBOB
	States
	{
	Spawn:
		BFGC BC 5 Bright 
		Loop
	}
}

ACTOR ArgBFGAmmoPickup : CustomInventory
{
	Inventory.PickupSound "items/ammo"
	Inventory.PickupMessage "$ARG_GOT_BFGCELL"
	Scale 0.66
	+FLOATBOB
	States
	{
	Spawn:
		BFGC BC 5 Bright 
		Loop
	Pickup:
		TNT1 A 0 A_GiveInventory("ArgBFGAmmo", 1)
		TNT1 A 0 A_GiveInventory("Cell"      ,60)
		Stop
	}
}

ACTOR ArgBFGAmmoSpawner replaces CellPack
{
	Scale 0.66
	+FORCEYBILLBOARD
	States
	{
	Spawn:
		BFGC A 0 NoDelay {
			// spawn a cellpack and destroy oneself if the BFG Ammotype CVAR
			// isn't enabled.
			if(GetCvar("arg_bfgammotype") == false) {
				A_SpawnItemEx("ArgCellPack",0,0,0,0,0,0,0,SXF_TRANSFERSPECIAL);
				return state("Vanish");
			} else {
				A_SpawnItemEx("ArgBFGAmmoPickup",0,0,0,0,0,0,0,SXF_ISTRACER|SXF_TRANSFERSPECIAL);
			}
			return state(0);
		}
	Idle:
		BFGC A 1
		BFGC A 1 {
			if(IsPointerEqual(AAPTR_TRACER, AAPTR_NULL)) {
				return state("Vanish");
			}
			return state(0);
		}
		Wait
	Vanish:
		TNT1 A 0
		Stop
	}
}

// --------------------------------------------------------------------------
//
// ARGENT: Combat Shotgun
//
// --------------------------------------------------------------------------

CONST int SHOTGUN_MOD1_AMMOUSE = 3;
CONST int SHOTGUN_MOD2_AMMOUSE = 2;
CONST int SHOTGUN_MOD_COOLDOWN = 90;

ACTOR ArgShotgunModSelected : ArgModToken {}
ACTOR ArgShotgunMod1Enabled : ArgToken    {}
ACTOR ArgShotgunMod2Enabled : ArgToken    {}
ACTOR ArgShotgunModCooldown : ArgToken    { Inventory.MaxAmount 90 }

ACTOR ArgShotgun : ArgModWeapon replaces Shotgun
{
	Weapon.SelectionOrder 6
	Weapon.AmmoUse 1
	Weapon.AmmoGive 8
	Weapon.AmmoType "Shell"
	Inventory.Icon "graphics/hud/weapon-shotgun.png"
	Inventory.PickupMessage "$ARG_GOT_SHOTGUN"
	Tag "$ARG_TAG_SHOTGUN"
	Obituary "$ARG_OB_SHOTGUN"
	DamageType "ArgShotgun"

	States
	{
	Spawn:
		SHTP A -1
		Stop
	Ready:
		SHTG A 0
		Goto Super::Ready

	/* Unmodded + Primary */

	Mod0Select:
		SHTG A 0
		Goto SelectLoop
	Mod0Swap:
		SH0G A 0
		Goto ModSwapUp
	Mod0Ready:
		SHTG A 0
		Goto ReadyLoop
	Mod0Fire:
		"####" B 1 Bright {
			A_GunFlash;
			A_FireBullets(5, 2, 7, 5, "ArgBulletPuff");
			A_PlaySound("weapons/shotgun/fire", CHAN_5);
			A_AlertMonsters;
		}
		"####" C 1 Bright A_WeaponOffset(4.0, 34.0)
		"####" D 1 A_WeaponOffset( 8.0, 34.0)
		"####" E 1 A_WeaponOffset(12.0, 34.0)
		"####" F 1 A_WeaponOffset(16.0, 34.0) 
		"####" F 1 A_WeaponOffset(17.0, 36.0)
		"####" E 1 A_WeaponOffset(15.0, 38.0) 
		"####" D 1 A_WeaponOffset(12.5, 40.0)
		"####" A 1 A_WeaponOffset(10.0, 38.0) 
		"####" A 1 A_WeaponOffset( 8.0, 36.5) 
		"####" A 1 A_WeaponOffset( 6.0, 35.0)
		"####" A 1 A_WeaponOffset( 5.0, 34.0) 
		"####" G 1 A_WeaponOffset( 3.2, 33.0) 
		"####" I 1 A_WeaponOffset( 2.4, 34.0) 
		"####" J 1 A_WeaponOffset( 3.6, 35.0) 
		"####" K 1 A_WeaponOffset( 4.8, 36.0) 
		"####" L 1 A_WeaponOffset( 6.0, 37.0) 
		"####" L 3 A_WeaponOffset( 7.2, 38.0) 
		"####" L 1 A_WeaponOffset( 6.0, 37.0) 
		"####" K 1 A_WeaponOffset( 4.8, 36.0) 
		"####" I 1 A_WeaponOffset( 3.6, 35.0) 
		"####" H 1 A_WeaponOffset( 2.4, 34.0) 
		"####" G 1 A_WeaponOffset( 1.2, 33.0) 
		"####" A 6 A_WeaponOffset( 0.0, 32.0) 
		"####" A 0 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 2 Bright A_Light1
		TNT1 A 4 Bright A_Light2
		TNT1 A 2 Bright A_Light1
		Goto LightDone
		
	/* Mod 1: Charged Burst */
		
	Mod1Select:
		SH1G A 0
		Goto SelectLoop
	Mod1Raise:
		SH0G A 0 {
			A_PlaySound("weapons/shotgun/mod1swap", CHAN_AUTO);
			A_WeaponOffset(-15.0, 0.0, WOF_KEEPY);
		}
		Goto ModSwapUp
	Mod1Swap:
		SH0G A 3
		SH1S ABC 1 
		SH1S D 1 A_WeaponOffset(-14.5, 32.0)
		SH1S E 1 A_WeaponOffset(-14.0, 32.0)
		SH1S F 1 A_WeaponOffset(-13.0, 32.0)
		SH1S G 1 A_WeaponOffset(-12.0, 32.0)
		SH1S H 1 A_WeaponOffset(-8.0, 32.0)
		SH1S H 1 A_WeaponOffset(-3.0, 32.5)
		SH1S H 1 A_WeaponOffset(-1.0, 33.0)
		SH1S H 1 A_WeaponOffset(-0.5, 32.5)
		SH1S H 1 A_WeaponOffset( 0.0, 32.0)
		SH1S H 1 A_WeaponOffset( 0.0, 32.0)
		SH1S I 1 A_WeaponOffset( 1.0, 33.0)
		SH1S J 1 A_WeaponOffset( 2.0, 34.0)
		SH1S K 1 A_WeaponOffset( 3.0, 35.0)
		SH1S L 1 A_WeaponOffset( 4.0, 36.0)
		SH1S M 1 A_WeaponOffset( 5.0, 37.0)
		SH1S M 1 A_WeaponOffset( 4.0, 36.0)
		SH1S M 1 A_WeaponOffset( 3.0, 35.0)
		SH1S M 1 A_WeaponOffset( 2.0, 34.0)
		SH1S M 1 A_WeaponOffset( 1.0, 33.0)
		SH1S M 3 A_WeaponOffset( 0.0, 32.0)
		SH1S N 1 A_WeaponOffset( 0.0, 32.0)
		SH1S N 1 A_WeaponOffset( 0.0, 32.0)
		SH1S O 2 A_WeaponOffset( 0.0, 32.0)
		SH1S P 2
	Mod1Ready:
		SH1G A 0
		Goto ReadyLoop
	Mod1Up:
		SH1G A 1 {
			if(CountInv("Shell") < SHOTGUN_MOD1_AMMOUSE) { return state("Ready"); }
			A_PlaySound("weapons/shotgun/mod1up", CHAN_AUTO);
			A_WeaponOffset(-2.0, 32.0);
			return state(0);
		}
		SH1G A 1 A_WeaponOffset(- 4.0, 32.0)
		SH1M A 1 A_WeaponOffset(- 8.0, 32.0)
		SH1M C 1 A_WeaponOffset(-10.0, 32.0)
		SH1M C 1 A_WeaponOffset(-11.0, 32.0)
		SH1M DE 2
	Mod1Hold:
		SH1M E 1 A_JumpIf(GetPlayerInput(INPUT_BUTTONS) & BT_ATTACK, "Mod1Fire")
		SH1M E 0 A_Refire
	Mod1Down:
		SH1M B 1 { A_WeaponOffset(-10.0, 32.0); A_PlaySound("weapons/shotgun/mod1down", CHAN_AUTO); }
		SH1M A 1   A_WeaponOffset(- 8.0, 32.0)
		SH1G A 1   A_WeaponOffset(- 4.0, 32.0)
		SH1G A 1   A_WeaponOffset(- 2.0, 32.0)
		SH1G A 1   A_WeaponOffset(  0.0, 32.0)
		Goto Ready
	Mod1Fire:
		SH1N A 1 Bright {
			A_GunFlash;
			A_AlertMonsters;
			A_FireBullets(5, 2, 7, 5, "ArgBulletPuff");
			A_TakeInventory("Shell", 1, TIF_NOTAKEINFINITE);
			A_GiveInventory("ArgShotgunModCooldown", SHOTGUN_MOD_COOLDOWN);
			A_PlaySound("weapons/shotgun/mod1fire", CHAN_5);
		}
		SH1N B 1 A_WeaponOffset(8.0, 36.0)
		SH1N C 1 A_WeaponOffset(4.0, 34.0)
		SH1N D 1 Bright {
			A_GunFlash;
			A_FireBullets(5, 2, 7, 5, "ArgBulletPuff");
			A_TakeInventory("Shell", 1, TIF_NOTAKEINFINITE);
			A_WeaponOffset(4.0, 34.0);
		}
		SH1N E 1 A_WeaponOffset(12.0, 38.0)
		SH1N F 1 A_WeaponOffset( 8.0, 36.0)
		SH1N G 1 Bright {
			A_GunFlash;
			A_FireBullets(5, 2, 7, 5, "ArgBulletPuff");
			A_TakeInventory("Shell", 1, TIF_NOTAKEINFINITE);
			A_WeaponOffset(8.0, 38.0);
		}
		SH1N H 1 A_WeaponOffset(13.0, 40.0)
		SH1G E 1 A_WeaponOffset(14.0, 40.0)
		SH1G F 1 A_WeaponOffset(16.0, 40.0) 
		SH1G F 1 A_WeaponOffset(17.0, 40.0)
		SH1G E 1 A_WeaponOffset(15.0, 40.0) 
		SH1G D 1 A_WeaponOffset(12.5, 40.0)
		SH1G A 1 A_WeaponOffset(10.0, 38.0) 
		SH1G A 1 A_WeaponOffset( 8.0, 36.5) 
		SH1G A 1 A_WeaponOffset( 6.0, 35.0)
		SH1G A 1 A_WeaponOffset( 5.0, 34.0) 
		SH1G A 1 A_WeaponOffset( 3.0, 33.0) 
		SH1G A 1 A_WeaponOffset( 1.0, 33.5) 
		SH1G A 6 A_WeaponOffset( 0.0, 32.0) 
		Goto Cooldown
		
	/* Mod 2: Explosive Shot */

	Mod2Select:
		SH2G A 0
		Goto SelectLoop
	Mod2Raise:
		SH2S A 0 A_WeaponOffset(-10.0, 0.0, WOF_KEEPY)
		Goto ModSwapUp
	Mod2Swap:
		SH2S B 3 A_PlaySound("weapons/shotgun/mod2swap", CHAN_AUTO)
		SH2S CDEFG 1 
		SH2S H 1 A_WeaponOffset(-9.5, 32.0)
		SH2S I 1 A_WeaponOffset(-9.0, 32.0)
		SH2S J 1 A_WeaponOffset(-8.0, 32.0)
		SH2S K 1 A_WeaponOffset(-6.0, 32.0)
		SH2S L 1 A_WeaponOffset(-4.0, 32.0)
		SH2S L 6 A_WeaponOffset(-3.0, 32.0)
		SH2S M 1 A_WeaponOffset(-2.0, 33.0)
		SH2S N 1 A_WeaponOffset(-1.0, 34.0)
		SH2S O 1 A_WeaponOffset( 0.0, 35.0)
		SH2S O 1 A_WeaponOffset( 1.0, 36.0)
		SH2S O 1 A_WeaponOffset( 2.0, 37.0)
		SH2S O 1 A_WeaponOffset( 1.0, 36.0)
		SH2S O 4 A_WeaponOffset( 0.0, 35.0)
		SH2S P 1 A_WeaponOffset( 2.0, 35.5)
		SH2S P 1 A_WeaponOffset( 4.0, 37.0)
		SH2G A 1 A_WeaponOffset( 3.0, 35.0)
		SH2G A 1 A_WeaponOffset( 2.0, 34.0)
		SH2G A 1 A_WeaponOffset( 1.0, 33.0)
	Mod2Ready:
		SH2G A 0
		Goto ReadyLoop
	Mod2Up:
		SH2G A 1 {
			if(CountInv("Shell") < SHOTGUN_MOD2_AMMOUSE) { return state("Ready"); }
			A_PlaySound("weapons/shotgun/mod2up", CHAN_AUTO);
			A_WeaponOffset(-2.0, 32.0);
			return state(0);
		}
		SH2G A 1 A_WeaponOffset(- 4.0, 32.0)
		SH2M A 1 A_WeaponOffset(- 8.0, 32.0)
		SH2M B 1 A_WeaponOffset(-10.0, 32.0)
		SH2M B 1 A_WeaponOffset(-11.0, 32.0)
	Mod2Hold:
		SH2M B 1 A_JumpIf(GetPlayerInput(INPUT_BUTTONS) & BT_ATTACK, "Mod2Fire")
		SH2M B 0 A_Refire
	Mod2Down:
		SH2M B 1 { A_WeaponOffset(-10.0, 32.0); A_PlaySound("weapons/shotgun/mod2down", CHAN_AUTO); }
		SH2M A 1   A_WeaponOffset(- 8.0, 32.0)
		SH2G A 1   A_WeaponOffset(- 4.0, 32.0)
		SH2G A 1   A_WeaponOffset(- 2.0, 32.0)
		SH2G A 1   A_WeaponOffset(  0.0, 32.0)
		Goto Ready
	Mod2Fire:
		SH2N A 1 Bright {
			A_GunFlash;
			A_AlertMonsters;
			A_FireCustomMissile("ArgShotgunExplosiveShot", 0, 0, 4,-4, FPF_AIMATANGLE , 5);
			A_TakeInventory("Shell", SHOTGUN_MOD2_AMMOUSE, TIF_NOTAKEINFINITE);
			A_GiveInventory("ArgShotgunModCooldown", SHOTGUN_MOD_COOLDOWN);
			A_PlaySound("weapons/shotgun/mod2fire", CHAN_5);
		}
		SH2M B 1 Bright A_WeaponOffset(4.0, 34.0)
		// technically copypasta'd pump sequence, but it's the easiest way
		// to avoid being able to trigger the cooldown script twice, so...
		SH2G D 1 A_WeaponOffset( 8.0, 34.0)
		SH2G E 1 A_WeaponOffset(12.0, 34.0)
		SH2G F 1 A_WeaponOffset(16.0, 34.0) 
		SH2G F 1 A_WeaponOffset(17.0, 36.0)
		SH2G E 1 A_WeaponOffset(15.0, 38.0) 
		SH2G D 1 A_WeaponOffset(12.5, 40.0)
		SH2G A 1 A_WeaponOffset( 8.0, 36.5) 
		SH2G A 1 A_WeaponOffset( 5.0, 34.0) 
		SH2G G 1 A_WeaponOffset( 3.2, 33.0) 
		SH2G I 1 A_WeaponOffset( 2.4, 34.0) 
		SH2G J 1 A_WeaponOffset( 3.6, 35.0) 
		SH2G K 1 A_WeaponOffset( 4.8, 36.0) 
		SH2G L 1 A_WeaponOffset( 6.0, 37.0) 
		SH2G L 3 A_WeaponOffset( 7.2, 38.0) 
		SH2G L 1 A_WeaponOffset( 6.0, 37.0) 
		SH2G K 1 A_WeaponOffset( 4.8, 36.0) 
		SH2G I 1 A_WeaponOffset( 3.6, 35.0) 
		SH2G H 1 A_WeaponOffset( 2.4, 34.0) 
		SH2G G 1 A_WeaponOffset( 1.2, 33.0) 
		SH2G A 6 A_WeaponOffset( 0.0, 32.0) 
		Goto Cooldown
	}
}

ACTOR ArgShotgunExplosiveShot
{
	Projectile
	Radius 8
	Height 8
	Damage 10
	Speed 50
	DamageType "ArgShotgunExplosiveShot"
	DeathSound "weapons/shotgun/mod2hit"
	Decal "Scorch"
	Obituary "$ARG_OB_SHOTGUN"

	-NOGRAVITY
	+EXTREMEDEATH

	States
	{
	Spawn:
		SSHT A 1 Bright A_SpawnItemEx("ArgShotgunExplosiveShotTrail", 0, 0, 0, 0, frandom(-0.33, 0.33), frandom(-0.33, 0.33))
		Loop
	Death:
		SEXP A 2 Bright {
			A_Explode(100, 128, 1);
			A_NoGravity;
		}
		SEXP BC 3 Bright
		SEXP DEF 2 Bright
		SEXP G 2 Bright A_SetTranslucent(0.7)
		SEXP H 2 Bright A_SetTranslucent(0.6)
		SEXP I 2 Bright A_SetTranslucent(0.5)
		SEXP J 2 Bright A_SetTranslucent(0.4)
		Stop
	}
}

ACTOR ArgShotgunExplosiveShotTrail : ArgTrail
{
	RenderStyle Add
	Alpha 0.5
	Scale 0.25
	States
	{
	Spawn:
		RTRL HHABCDEFG 1 Bright
		Stop
	}
}
